10 _TURBO ON
20 DIM CL%(12),SCL%(12),FLD%(35,12),PLT%(12,6),FCOL%(7),ACT%(2),CP%(2),TTL%(2),RS%(4,2)
30 CM%=7:CR=0
40 FOR I%=1 TO 7+4 : READ CL%(I%), PLT%(I%,1), PLT%(I%,2), PLT%(I%,3), SCL%(I%), PLT%(I%,4), PLT%(I%,5), PLT%(I%,6):NEXT I%
50 'COLOR 15,4,4:  C=6000:GOSUB 1680:FOR I%=6 TO 1 STEP -1 :COLOR=(4,1,1,I%):COLOR=(15,I%,I%,I%):beep:GOSUB 1680 :NEXT I%
60 COLOR 15,0,0:CLS
70 SCREEN 5:DEFINT A-Z
80 COLOR=(4,1,1,7):COLOR=(15,7,7,7)
90 ACT%(1)=8:ACT%(2)=10
100 SET PAGE 0,1:COLOR 15,0,0:CLS
110 FOR I%=1 TO 11
120 C=CL%(I%): S=SCL%(I%): X = I%*15-10: Y=200
130 GOSUB 1050
140 NEXT I%
150 DATA 8,7,1,1,6,5, 1,1,10,6,6,1,14, 5,5,5,14,5,5,5, 14,5,5,5,3,3,7,3, 2,1,6,1,7,2,6,7, 5,2,3,7,5,2,3,7,4,1, 1,7,13,6,2,5,6,5,1,1, 9,0,0,0,12,5,1,1,9,1, 1,1,12,5,1,1,1,2,2,2, 11,5,1,1,1,3,3,3,11, 5,1,1
160 'OPEN "GRP:" AS #1
170 X=9:STP=6:Y=4
180 C=4:A$="F":GOSUB 1590:X=X+STP-1
190 C=2:A$="i":GOSUB 1590:X=X+STP-1
200 C=6:A$="l":GOSUB 1590:X=X+STP-1
210 C=1:A$="l":GOSUB 1590:X=X+STP
220 C=7:A$="e":GOSUB 1590:X=X+STP+1
230 C=5:A$="r":GOSUB 1590
240 SET PAGE 0,0
250 LINE(10,100)-(240,115),,B
260 OLDC=1
270 IF INKEY$<>"" THEN GOTO 270 'clear keyboard buffer
280 C=15:X=0:Y=200:A$="  > (c)Comissar 2022, v1.07 <  ":GOSUB 1590
285 LINE (166,200)-(171,202),4,BF
287 LINE (166,203)-(171,205),10,BF
290 FOR J=4 TO 10
300 FOR I=8 TO 42
310 SET PAGE 0,1 : C=POINT(I,J): SET PAGE 0,0
320 IF C<>0 THEN X=I*7-58:Y=J*9-10:GOSUB 1100
330 IF INKEY$<>"" THEN LINE(0,0)-(250,95),0,BF:I=42:J=10
340 NEXT I : LINE(15,105)-(J*32-86,110),15,BF
350 NEXT J
360 PC%=1:' Choose 1 or 2 player mode to pc%
370 LINE (40,155)-(210,182),15,B
380 C=15:X=60:Y=160:A$="  1 player  ":GOSUB 1590:COPY(60,160)-STEP(120,12) TO (59,160),,TPSET
390 X=60:Y=170:A$="  2 players     ":GOSUB 1590:COPY(60,170)-STEP(120,12) TO (59,170),,TPSET
400 IF PC%=1 THEN SS$=" >":SQ$="  ": ELSE SQ$=" >":SS$="  "
410 X=52:Y=160:A$=SS$:GOSUB 1590:COPY(52,160)-STEP(10,12) TO (51,160),,TPSET 
420 X=52:Y=170:A$=SQ$:GOSUB 1590:COPY(52,170)-STEP(10,12) TO (51,170),,TPSET
430 gosub 1720:A$=INKEY$: GOSUB 1640: IF A$="" AND RS%(1,2)=0 AND RS%(2,2)=0 AND RS%(3,2)=0 AND RS%(4,2)=0 THEN GOTO 430 ELSE IF A$<>" " AND ASC(A$)<>13 AND RS%(1,2)=0 AND RS%(2,2)=0 THEN PC%=3-PC%: GOTO 400
440 G=RND(-TIME)
450 C=15:X=0:Y=140:A$="  >   Generating crystals   <  ":GOSUB 1590
460 FOR I%=1 TO 34
470 FOR J%=1 TO 11
480 FLD%(I%,J%) = INT(RND(1)*7+1)
490 NEXT J%:COLOR 15:LINE(15,105)-(I%*6+30,110),4,BF
500 NEXT I%
510 BIGAREA=90 '500 for big areas
520 C=15:X=0:Y=140:A$="  >     Area prepairing     <  ":GOSUB 1590
530 FOR I%=2 TO BIGAREA
540 IF I% MOD 10 = 0 THEN LINE(15,105)-(24*I%/10+15,110),15,BF
550 X%=RND(1)*(34-2)+2:Y%=RND(1)*(11-2)+2:
560 IF I% MOD 2 = 0 THEN FLD%(X%,Y%)=FLD%(X%-1,Y%-1) ELSE FLD%(X%,Y%)=FLD%(X%-1,Y%)
570 NEXT I%
580 C=15:X=0:Y=140:A$="  >     Prepairing field    <  ":GOSUB 1590
590 FLD%(1,11)=8:FLD%(34,1)=10:NI%=34:NJ%=1
600 TTL%(1)=1:TTL%(2)=1
610 ST=1:FOR K%=7 TO 1 STEP -1:IF FLD%(2,11) <> K% AND FLD%(33,1)<>K% THEN FCOL%(ST)=K%:ST=ST+1
620 NEXT K%
630 K%=INT(RND(TIME)*(7-2))+1:CP(1)=FCOL%(K%):CP(2)=FCOL%(((K%+2) MOD (7-2))+1)
640 COLOR=(CL%(ACT(1)),PLT%(CP(1),1),PLT%(CP(1),2),PLT%(CP(1),3)):COLOR=(SCL%(ACT(1)),PLT%(CP(1),4),PLT%(CP(1),5),PLT%(CP(1),6))
650 COLOR=(CL%(ACT(2)),PLT%(CP(2),1),PLT%(CP(2),2),PLT%(CP(2),3)):COLOR=(SCL%(ACT(2)),PLT%(CP(2),4),PLT%(CP(2),5),PLT%(CP(2),6))
660 SET PAGE 0,0:COLOR 15,0,0:CLS
670 FOR I%=1 TO 7 'draw color selectors
680 C=CL%(I%): S=SCL%(I%): X = I%*15-10: Y=200
690 GOSUB 1050 : IF PC%=2 THEN X=X+140: GOSUB 1050
700 NEXT I%
710 FOR I%=1 TO 34 ' draw field
720 FOR J%=1 TO 11 
730 GOSUB 1010
740 NEXT J% 
750 NEXT I%
760 TURN=2
770 TURN=3-TURN 'start turn
780 IF PC%=1 AND TURN=2 THEN GOSUB 1330
790 IF PC%=2 OR TURN=1 THEN GOSUB 1190
800 CP(TURN)=IC%
810 IF TURN=1 THEN ST=1:EN=34:S=1 ELSE ST=34:EN=1:S=-1
820 COLOR=(CL%(ACT(TURN)),PLT%(IC%,1),PLT%(IC%,2),PLT%(IC,3))
830 COLOR=(SCL%(ACT(TURN)),PLT%(IC%,4),PLT%(IC%,5),PLT%(IC%,6))
840 C=IC%: X = 95-90*S: Y=10:GOSUB 1100
850 AD%=0
860 FOR I%=ST TO EN STEP S
870 S1%=0
880 FOR J%=11 TO 1 STEP -1
890 IF S1%=0 AND FLD%(I%,J%)=ACT(TURN) THEN S1%=1
900 IF FLD%(I%,J%)=IC% THEN GOSUB 1130
910 NEXT J%
920 IF S1%=0 THEN I%=EN
930 NEXT I%
940 IF AD%<>0 THEN 850
950 C=15:X=110-90*S:Y=5:A$=STR$(TTL%(TURN)):GOSUB 1590
960 'beep:'a$=input$(1)
965 IF TTL%(1) = 11*34/2 AND TTL%(2) = 11*34/2 THEN LINE (60,60)-(200,90),0,BF:LINE (60,60)-(200,90),15,B:X=100:Y=72:A$="D R A W !":GOSUB 1590:COPY(80,72)-STEP(100,12) TO (79,72),,TPSET:GOTO 980
970 IF TTL%(TURN) > 11*34/2 THEN LINE (60,60)-(200,90),0,BF:LINE (60,60)-(200,90),15,B:X=80:Y=72:A$="Player"+STR$(TURN)+" win!":GOSUB 1590:COPY(80,72)-STEP(100,12) TO (79,72),,TPSET:ELSE GOTO 990 
980 A$=INKEY$:GOSUB 1640:IF A$="" AND STRIG(1)=0 AND STRIG(2)=0 AND STRIG(3)=0 AND STRIG(4)=0 THEN GOTO 980 ELSE SET PAGE 0,0: CLS:SET PAGE 0,1:GOTO 170
990 GOTO 770
1000 END
1010 C = FLD%(I%,J%) 
1020 X = I%*7-3: Y=J%*14+7*((I%) MOD 2)+15: GOTO 1100
1030 C = FLD%(K%,L%)
1040 X = K%*7-3: Y=L%*14+7*((K%) MOD 2)+15: GOTO 1100
1050 LINE(X,Y)-(X+6,Y-6),S:LINE(X+6,Y-6)-(X+12,Y),C:LINE(X+12,Y)-(X+6,Y+6),C:LINE(X,Y)-(X+6,Y+6),C':DRAW "c=c; bm=x;,=y;"
1060 LINE(X+1,Y)-(X+6,Y-5),C:LINE(X+6,Y-5)-(X+11,Y),C:LINE(X+11,Y)-(X+6,Y+5),C:LINE(X+1,Y)-(X+6,Y+5),C
1070 LINE (X+3,Y+3)-(X+9,Y-3),C:LINE (X+4,Y+3)-(X+9,Y-2),C:LINE (X+3,Y+2)-(X+8,Y-3),C:PAINT(X+4,Y),15,C:PAINT(X+9,Y),S,C
1080 'DRAW "c=s; e6 c=c; f6g6h6 br1 c=s; e5 c=c;f5g5h5 c=s; br1bf4 e4h1g4r1e3h1 c=c; g4l1e5 c15 l1 g4u1e4 l1 g4 u1 c=c; e4"
1090 RETURN
1100 COPY (C*15-10, 194)-STEP(12,12),1 TO (X,Y-6),,TPSET:RETURN
1110 IF PC%=1 AND TURN=2 AND NI%>I% THEN NI%=I%:NJ%=J%
1120 FLD%(I%,J%)=ACT(TURN):AD%=1:S1%=1:TTL%(TURN)=TTL%(TURN)+1:GOSUB 1010:RETURN
1130 'check neigboard of i% j% for act(turn)
1140 K%=I%+1:L%=J%: IF FLD%(K%,L%)=ACT(TURN) THEN GOSUB 1110:RETURN
1150 K%=I%+1:L%=J%+1-2*((I%+1) MOD 2):IF FLD%(K%,L%)=ACT(TURN) THEN GOSUB 1110:RETURN
1160 K%=I%-1:L%=J%:IF FLD%(K%,L%)=ACT(TURN) THEN GOSUB 1110:RETURN
1170 K%=I%-1:L%=J%+1-2*((I%+1) MOD 2):IF FLD%(K%,L%)=ACT(TURN) THEN GOSUB 1110:RETURN
1180 RETURN
1190 'get color for turn without cp(1) and cp(2) return ic
1200 CI%=1:BEEP:DC=1
1210 IF CP(1)=CI% OR CP(2)=CI% THEN CI%=CI%+DC : GOTO 1210
1220 IF CI%>7 THEN CI%=1 : GOTO 1210
1230 IF CI%<1 THEN CI%=7 : GOTO 1210
1240 X=CI%*15 + (TURN) * 140 - 150: LINE(X,208)-(X+12,208),CL%(CI%)
1250 GOSUB 1720:A$=INKEY$: GOSUB 1640: IF A$="" AND RS%(1,2)=0 AND RS%(2,2)=0 AND RS%(3,2)=0 AND RS%(4,2)=0 THEN GOTO 1250
1260 COLOR 0: X=CI%*15 + (TURN) * 140 - 150: LINE(X,208)-(X+12,208),0:COLOR 15
1270 IF A$=" " OR ASC(A$)=13 OR RS%(1,2)<>0 OR RS%(2,2)<>0 THEN IC%=CI% :COLOR=(15,7,7,7): RETURN
1280 IF A$="Z" OR A$="z" OR A$="Q" OR a$="q" or A$=CHR$(29) THEN DC=-1 ELSE DC=1
1290 CI%=CI%+DC: IF CI%>7 THEN CI%=1 
1300 IF CI%<1 THEN CI%=7
1310 GOTO 1210
1320 RETURN
1330 'return IC% for 2nd player
1340 Y1%=0:Y2%=0:Y3=0:Y4=0
1350 IF CP(1)<>FLD%(NI%-1,NJ%) AND 0<>FLD%(NI%-1,NJ%) AND 7>=FLD%(NI%-1,NJ%)THEN I1%=FLD%(NI%-1,NJ%):Y1%=NJ%
1360 IF CP(1)<>FLD%(NI%-1,NJ%+1-2*((NI%+1) MOD 2)) AND 0 <> FLD%(NI%-1,NJ%+1-2*((NI%+1) MOD 2)) AND 7 >= FLD%(NI%-1,NJ%+1-2*((NI%+1) MOD 2))THEN I2%=FLD%(NI%-1,NJ%+1-2*((NI%+1) MOD 2)):Y2%=NJ%+1-2*((NI%+1) MOD 2)
1370 IF CP(1)<>FLD%(NI%+1,NJ%) AND 0<>FLD%(NI%+1,NJ%) AND 7>=FLD%(NI%+1,NJ%)THEN I3%=FLD%(NI%+1,NJ%):Y3%=NJ%
1380 IF CP(1)<>FLD%(NI%+1,NJ%+1-2*((NI%+1) MOD 2)) AND 0 <> FLD%(NI%+1,NJ%+1-2*((NI%+1) MOD 2)) AND 7 >= FLD%(NI%+1,NJ%+1-2*((NI%+1) MOD 2))THEN I4%=FLD%(NI%+1,NJ%+1-2*((NI%+1) MOD 2)):Y4%=NJ%+1-2*((NI%+1) MOD 2)
1390 IF Y1%=0 AND Y2%=0 THEN GOTO 1420
1400 IF Y1%>Y2% THEN IC%=I1% ELSE IC%=I2
1410 RETURN
1420 IF Y3%=0 AND Y4%=0 THEN GOTO 1450
1430 IF Y3%>Y4% THEN IC%=I3% ELSE IC%=I4
1440 RETURN
1450 Y1%=0:FOR I1=NI% TO 34: FOR I2=11 TO 1 STEP -1
1460 IF FLD%(I1,I2) = ACT(2) AND (0<>FLD%(I1%-1,I2%) AND 7>=FLD%(I1%-1,I2%) AND CP(1)<>FLD%(I1%-1,I2%)) THEN NI%=I1:NJ%=I2:Y1%=1: GOTO 1510
1470 IF FLD%(I1,I2) = ACT(2) AND (0<>FLD%(I1%-1,I2%+1-2*((I1%+1) MOD 2)) AND 7>=FLD%(I1%-1,I2%+1-2*((I1%+1) MOD 2)) AND CP(1)<>FLD%(I1%-1,I2%+1-2*((I1%+1) MOD 2))) THEN NI%=I1:NJ%=I2:Y1%=1: GOTO 1510
1480 IF FLD%(I1,I2) = ACT(2) AND (0<>FLD%(I1%+1,I2%) AND 7>=FLD%(I1%+1,I2%) AND CP(1)<>FLD%(I1%+1,I2%)) THEN NI%=I1:NJ%=I2:Y1%=1: GOTO 1510
1490 IF FLD%(I1,I2) = ACT(2) AND (0<>FLD%(I1%+1,I2%+1-2*((I1%+1) MOD 2)) AND 7>=FLD%(I1%+1,I2%+1-2*((I1%+1) MOD 2)) AND CP(1)<>FLD%(I1%+1,I2%+1-2*((I1%+1) MOD 2))) THEN NI%=I1:NJ%=I2:Y1%=1: GOTO 1510
1500 GOTO 1520
1510 I1=34:I2=1
1520 NEXT I2:NEXT I1:IF Y1%<>0 THEN GOTO 1340
1530 IC%=INT(RND(1)*7+1)
1540 IF CP(1)=IC% THEN IC%=IC%+1 
1550 IF IC%>7 THEN IC%=1:GOTO 1540
1560 IF CP(2)=IC% THEN IC%=IC%+1:GOTO 1540
1570 IF IC%>7 THEN IC%=1 : GOTO 1540
1580 RETURN
1590 ' SUB Put_String(a$,x,y,c)
1600 COLOR C:PRESET(X,Y):FOR I=1 TO LEN(A$):K=ASC(MID$(A$,I,1))
1610 '#I &h3A,K
1620 '#I &HCD,&H8D,&H00
1630 NEXT I:RETURN
1640 CR=CR+1: IF CR<1000 THEN RETURN 
1650 CR=0:IF CM%=7 THEN CM%=6 ELSE CM%=7
1660 COLOR=(15,CM%,CM%,CM%)
1670 RETURN
1680 'SUB Pause(c)
1690 A=TIME
1700 if ( TIME-A ) < c then goto 1700
1710 RETURN
1720 'SUB RealStrig() return RS%(device,is_pressed=1/is_released=2)
1730 FOR I=0 to 4
1740 A=STRIG(I):If A=0 and RS%(I,1)=-1 THEN RS%(I,2)=1 ELSE RS%(I,2)=0
1750 RS%(I,1)=A
1760 NEXT I
1770 RETURN