0 _TURBO ON
1 DEFINT A-Z:DIM CL(7),SCL(7),FLD(35,12),PLT(7,6),FCOL(7),ACT(2),CP(2),TTL(2),RS(4,2):DEFINT A-Z
2 CM=7:CR=0:DZ=1:BL=1
3 FOR I=1TO7:READ CL(I), PLT(I,1), PLT(I,2), PLT(I,3), SCL(I), PLT(I,4), PLT(I,5), PLT(I,6):COLOR=(CL(I),PLT(I,1),PLT(I,2),PLT(I,3)):COLOR=(SCL(I),PLT(I,4),PLT(I,5),PLT(I,6)):NEXT I
4 COLOR 15,0,0:CLS
5 SCREEN 5
6 COLOR=(4,1,1,7):COLOR=(15,7,7,7)
7 ACT(1)=8:ACT(2)=10:DZ=1:BL=1
8 SET PAGE 0,1:COLOR 15,0,0:CLS
9 FOR I=1 TO 7
10 C=CL(I):S=SCL(I):X=I*15-10:Y=200
11 GOSUB116
12 NEXT I
13 DATA 8,7,2,2,6,5,0,0, 10,7,7,2,14,5,5,2, 9,7,7,7,1,5,5,5, 3,2,7,2,2,0,5,0, 7,2,7,7,11,0,5,5, 5,2,2,7,4,0,0,5, 13,7,2,7,12,5,0,5
14 X=9:STP=6:Y=4
15 C=4:A$="F":GOSUB166:X=X+STP-1
16 C=2:A$="i":GOSUB166:X=X+STP-1
17 C=6:A$="l":GOSUB166:X=X+STP-1
18 C=1:A$="l":GOSUB166:X=X+STP
19 C=7:A$="e":GOSUB166:X=X+STP+1
20 C=5:A$="r":GOSUB166
21 SET PAGE 0,0
22 LINE(10,100)-(240,115),,B
23 IF INKEY$<>"" THEN GOTO 23
24 C=15:X=0:Y=200:A$="  > (c)Comissar 2022, v1.10 <  ":GOSUB166
25 LINE (166,200)-(171,202),4,BF
26 LINE (166,203)-(171,205),10,BF
27 FOR J=4 TO 10
28 FOR I=8 TO 42
29 SET PAGE 0,1 :C=POINT(I,J):SET PAGE 0,0
30 IF C<>0 THEN X=I*7-58:Y=J*9-10:GOSUB118
31 IF INKEY$<>"" THEN LINE(0,0)-(250,95),0,BF:I=42:J=10
32 NEXT I :LINE(15,105)-(J*32-86,110),15,BF
33 NEXT J
34 PC=1
35 LINE (40,145)-(210,182),15,B
36 LINE (41,146)-(209,181),0,BF
37 C=15:X=60:Y=150:A$="  1 player  ":GOSUB166
38 Y=160:A$="  2 players     ":GOSUB166
39 Y=170:A$="  Settings     ":GOSUB166:COPY(60,150)-STEP(120,32) TO (59,150),,TPSET
40 LINE(50,150)-(66,181),0,BF
41 Y=140+PC*10:A$=">":GOSUB166:COPY(52,150)-STEP(10,32) TO (51,150),,TPSET 
42 GOSUB180:A$=INKEY$:GOSUB171:IF A$="" AND RS(1,2)=0 AND RS(2,2)=0 AND RS(3,2)=0 AND RS(4,2)=0 THEN GOTO 42 ELSE IF A$<>" " AND ASC(A$)<>13 AND RS(1,2)=0 AND RS(2,2)=0 THEN PC=PCMOD3+1:GOTO 40
43 IF PC<>3 THEN GOTO 57 
44 LINE (41,146)-(209,181),0,BF
45 C=15:X=60:Y=150:IF DZ=1THEN A$="  Standart  " ELSE A$="  Classic  "
46 GOSUB166
47 X=60:Y=160:C=15:IF BL=1 THEN A$="  Blinking ON     " ELSE A$="  Blinking OFF    "
48 GOSUB166
49 X=60:Y=170:A$="  Back     ":GOSUB166:COPY(60,150)-STEP(120,32) TO (59,150),,TPSET:C=CL(4):S=SCL(4):X=170:Y=155:GOSUB116:C=15
50 LINE(50,150)-(66,181),0,BF
51 X=60:Y=140+PC*10:A$=">":GOSUB166:COPY(52,150)-STEP(10,32) TO (51,150),,TPSET 
52 GOSUB180:A$=INKEY$:GOSUB171:IF A$="" AND RS(1,2)=0 AND RS(2,2)=0 AND RS(3,2)=0 AND RS(4,2)=0 THEN GOTO 52 ELSE IF A$<>" " AND ASC(A$)<>13 AND RS(1,2)=0 AND RS(2,2)=0 THEN PC=PC MOD 3 + 1:GOTO 50
53 IF PC=1 THEN DZ=3-DZ
54 IF PC=2 THEN BL=3-BL:COLOR=(15,7,7,7)
55 IF PC=3 THEN SET PAGE 0,0:CLS:SET PAGE 0,1:GOTO 8
56 GOTO 44
57 G=RND(-TIME)
58 C=15:X=0:Y=130:A$="  >   Generating crystals   <  ":GOSUB166
59 FOR I=1 TO 34
60 FOR J=1 TO 11
61 FLD(I,J)=INT(RND(1)*7+1)
62 NEXT J:COLOR 15:LINE(15,105)-(I*6+30,110),4,BF
63 NEXT I
64 BIGAREA=90 '500 for big areas
65 C=15:X=0:Y=130:A$="  >     Area prepairing     <  ":GOSUB166
66 FOR I=2 TO BIGAREA
67 IF I MOD 10=0 THEN LINE(15,105)-(24*I/10+15,110),15,BF
68 X=RND(1)*(34-2)+2:Y=RND(1)*(11-2)+2:
69 IF I MOD 2=0 THEN FLD(X,Y)=FLD(X-1,Y-1) ELSE FLD(X,Y)=FLD(X-1,Y)
70 NEXT I
71 C=15:X=0:Y=130:A$="  >     Prepairing field    <  ":GOSUB166
72 FLD(1,11)=8:FLD(34,1)=10:NI=34:NJ=1
73 TTL(1)=1:TTL(2)=1
74 ST=1:FOR K=7 TO 1 STEP -1:IF FLD(2,11) <> K AND FLD(33,1)<>K THEN FCOL(ST)=K:ST=ST+1
75 NEXT K
76 K=INT(RND(TIME)*(7-2))+1:CP(1)=FCOL(K):CP(2)=FCOL(((K+2) MOD (7-2))+1)
77 SET PAGE 0,0:COLOR 15,0,0:CLS
78 FOR I=1 TO 7
79 C=CL(I):S=SCL(I):X=I*15-10:Y=200
80 SET PAGE 0,1:LINE(X-1,93)-(X+13,107),C,B:LINE(X+2,93)-(X+10,107),0,B :SET PAGE 0,0
81 GOSUB116 :IF PC=2 THEN X=X+140:GOSUB116
82 SET PAGE 0,1:LINE(X-1,93)-(X+13,107),C,B:LINE(X+2,93)-(X+10,107),0,B :SET PAGE 0,0
83 NEXT I
84 FOR I=1 TO 34
85 FOR J=1 TO 11 
86 GOSUB112
87 NEXT J,I
88 I=1:J=11:C=CP(1):GOSUB113:I=34:J=1:C=CP(2):GOSUB113
89 TURN=2
90 TURN=3-TURN
91 IF PC=1 AND TURN=2 THEN GOSUB140
92 IF PC=2 OR TURN=1 THEN GOSUB126
93 CP(TURN)=IC
94 IF TURN=1 THEN ST=1:EN=34:S=1 ELSE ST=34:EN=1:S=-1
95 C=IC:X=95-90*S:Y=10:GOSUB118
96 AD=0
97 FOR I=ST TO EN STEP S
98 S1=0
99 FOR J=11 TO 1 STEP -1
100 IF FLD(I,J)=ACT(TURN) THEN C=IC:GOSUB113:S1=1
101 IF FLD(I,J)=IC THEN GOSUB121
102 NEXT J
103 IF S1=0 THEN I=EN
104 NEXT I
105 IF AD<>0 THEN 96
106 C=15:X=110-90*S:Y=5:A$=STR$(TTL(TURN)):GOSUB166
107 IF TTL(1)=11*34/2 AND TTL(2)=11*34/2 THEN LINE (60,60)-(200,90),0,BF:LINE (60,60)-(200,90),15,B:X=100:Y=72:A$="D R A W !":GOSUB166:COPY(80,72)-STEP(100,12) TO (79,72),,TPSET:GOTO 109
108 IF TTL(TURN) > 11*34/2 THEN LINE (60,60)-(200,90),0,BF:LINE (60,60)-(200,90),15,B:X=80:Y=72:A$="Player"+STR$(TURN)+" win!":GOSUB166:COPY(80,72)-STEP(100,12) TO (79,72),,TPSET:ELSE GOTO 110 
109 A$=INKEY$:GOSUB171:IF A$="" AND STRIG(1)=0 AND STRIG(2)=0 AND STRIG(3)=0 AND STRIG(4)=0 THEN GOTO 109 ELSE SET PAGE 0,0:CLS:SET PAGE 0,1:GOTO 14
110 GOTO 90
111 END
112 C=FLD(I,J) 
113 X=I*7-3:Y=J*14+7*((I) MOD 2)+15:GOTO 118
114 C=FLD(K,L)
115 X=K*7-3:Y=L*14+7*((K) MOD 2)+15:GOTO 118
116 IF DZ=1 THEN GOSUB192 ELSE GOSUB186
117 RETURN
118 COPY (C*15-10, 193)-STEP(13,13),1 TO (X,Y-6),,TPSET:RETURN
119 IF PC=1 AND TURN=2 AND NI>I THEN NI=I:NJ=J
120 FLD(I,J)=ACT(TURN):AD=1:S1=1:TTL(TURN)=TTL(TURN)+1:RETURN
121 K=I+1:L=J:IF FLD(K,L)=ACT(TURN) THEN GOSUB119:RETURN
122 K=I+1:L=J+1-2*((I+1) MOD 2):IF FLD(K,L)=ACT(TURN) THEN GOSUB119:RETURN
123 K=I-1:L=J:IF FLD(K,L)=ACT(TURN) THEN GOSUB119:RETURN
124 K=I-1:L=J+1-2*((I+1) MOD 2):IF FLD(K,L)=ACT(TURN) THEN GOSUB119:RETURN
125 RETURN
126 'get color for turn without cp(1) and cp(2) return ic
127 CI=1:BEEP:DC=1
128 IF CP(1)=CI OR CP(2)=CI THEN CI=CI+DC :GOTO 128
129 IF CI>7 THEN CI=1 :GOTO 128
130 IF CI<1 THEN CI=7 :GOTO 128
131 X=CI*15 + (TURN) * 140 - 150:COPY(X-1,93)-(X+13,107),1 TO (X-1,193),,XOR
132 GOSUB180:A$=INKEY$:GOSUB171:IF A$="" AND RS(1,2)=0 AND RS(2,2)=0 AND RS(3,2)=0 AND RS(4,2)=0 THEN GOTO 132
133 COLOR 0:X=CI*15 + (TURN) * 140 - 150:COPY(X-1,93)-(X+13,107),1 TO (X-1,193),,XOR
134 IF A$=" " OR ASC(A$)=13 OR RS(1,2)<>0 OR RS(2,2)<>0 THEN IC=CI :COLOR=(15,7,7,7):RETURN 
135 IF A$="Z" OR A$="z" OR A$="Q" OR A$="q" OR A$=CHR$(29) THEN DC=-1 ELSE DC=1
136 CI=CI+DC:IF CI>7 THEN CI=1 
137 IF CI<1 THEN CI=7
138 GOTO 128
139 RETURN
140 '
141 Y1=0:Y2=0:Y3=0:Y4=0
142 K=NI-1:L=NJ:IF CP(1)<>FLD(K,L) AND 0<>FLD(K,L) AND 7>=FLD(K,L)THEN I1=FLD(K,L):Y1=L
143 K=NI-1:L=NJ+1-2*((NI+1)AND1):IF CP(1)<>FLD(K,L) AND 0 <> FLD(K,L) AND 7 >=FLD(K,L)THEN I2=FLD(K,L):Y2=L
144 K=NI+1:L=NJ:IF CP(1)<>FLD(K,L) AND 0<>FLD(K,L) AND 7>=FLD(K,L)THEN I3=FLD(K,L):Y3=L
145 K=NI+1:L=NJ+1-2*((NI+1)AND1):IF CP(1)<>FLD(K,L) AND 0 <> FLD(K,L) AND 7 >=FLD(K,L)THEN I4=FLD(K,L):Y4=L
146 IF Y1=0 AND Y2=0 THEN GOTO 149
147 IF Y1>Y2 THEN IC=I1 ELSE IC=I2
148 RETURN
149 IF Y3=0 AND Y4=0 THEN GOTO 152
150 IF Y3>Y4 THEN IC=I3 ELSE IC=I4
151 RETURN
152 Y1=0:FOR I1=NI TO 34:FOR I2=11 TO 1 STEP -1
153 IF FLD(I1,I2)=ACT(2) AND (0<>FLD(I1-1,I2) AND 7>=FLD(I1-1,I2) AND CP(1)<>FLD(I1-1,I2)) THEN NI=I1:NJ=I2:Y1=1:GOTO 158
154 IF FLD(I1,I2)=ACT(2) AND (0<>FLD(I1-1,I2+1-2*((I1+1) MOD 2)) AND 7>=FLD(I1-1,I2+1-2*((I1+1) MOD 2)) AND CP(1)<>FLD(I1-1,I2+1-2*((I1+1) MOD 2))) THEN NI=I1:NJ=I2:Y1=1:GOTO 158
155 IF FLD(I1,I2)=ACT(2) AND (0<>FLD(I1+1,I2) AND 7>=FLD(I1+1,I2) AND CP(1)<>FLD(I1+1,I2)) THEN NI=I1:NJ=I2:Y1=1:GOTO 158
156 IF FLD(I1,I2)=ACT(2) AND (0<>FLD(I1+1,I2+1-2*((I1+1) MOD 2)) AND 7>=FLD(I1+1,I2+1-2*((I1+1) MOD 2)) AND CP(1)<>FLD(I1+1,I2+1-2*((I1+1) MOD 2))) THEN NI=I1:NJ=I2:Y1=1:GOTO 158
157 GOTO 159
158 I1=34:I2=1
159 NEXT I2:NEXT I1:IF Y1<>0 THEN GOTO 141
160 IC=INT(RND(1)*7+1)
161 IF CP(1)=IC THEN IC=IC+1 
162 IF IC>7 THEN IC=1:GOTO 161
163 IF CP(2)=IC THEN IC=IC+1:GOTO 161
164 IF IC>7 THEN IC=1 :GOTO 161
165 RETURN
166 ' SUB Put_String(a$,x,y,c)
167 COLOR C:PRESET(X,Y):FOR I=1 TO LEN(A$):K=ASC(MID$(A$,I,1))
168 '#I &h3A,K
169 '#I &HCD,&H8D,&H00
170 NEXT I:RETURN
171 IF BL<>1 THEN RETURN 
172 CR=CR+1:IF CR<1000 THEN RETURN
173 CR=0:IF CM=7 THEN CM=6 ELSE CM=7
174 COLOR=(15,CM,CM,CM)
175 RETURN
176 'SUB Pause(c)
177 A=TIME
178 IF ( TIME-A ) < C THEN GOTO 178
179 RETURN
180 'SUB RealStrig() return RS(device,is_pressed=1/is_released=2)
181 FOR I=0 TO 4
182 A=STRIG(I):IF A=0 AND RS(I,1)=-1 THEN RS(I,2)=1 ELSE RS(I,2)=0
183 RS(I,1)=A
184 NEXT I
185 RETURN
186 LINE(X,Y)-(X+6,Y),S:LINE(X+6,Y)-(X+6,Y+6),S:LINE(X+6,Y+6)-(X,Y),S:PAINT(X+2,Y+1),S,S
187 LINE(X+7,Y-1)-(X+12,Y-1),S:LINE(X+12,Y-1)-(X+7,Y-6),S:LINE(X+7,Y-6)-(X+7,Y-1),S:PAINT(X+8,Y-2),S,S
188 LINE(X,Y-1)-(X+6,Y-1),C:LINE(X+6,Y-1)-(X+6,Y-7),C:LINE(X+6,Y-7)-(X,Y-1),C:PAINT(X+3,Y-2),C,C
189 LINE(X+7,Y)-(X+7,Y+6),S:LINE(X+7,Y+6)-(X+13,Y),S:LINE(X+13,Y)-(X+7,Y),S:PAINT(X+8,Y+1),S,S
190 PSET(X+7,Y),0:LINE(X+9,Y)-(X+7,Y+2),0:LINE(X+11,Y)-(X+7,Y+4),0:LINE(X+13,Y)-(X+7,Y+6),0
191 RETURN
192 LINE(X,Y)-(X+6,Y-6),S:LINE(X+6,Y-6)-(X+12,Y),C:LINE(X+12,Y)-(X+6,Y+6),C:LINE(X,Y)-(X+6,Y+6),C:LINE(X+1,Y)-(X+6,Y-5),C
193 PAINT(X+4,Y),C,C:LINE(X+2,Y)-(X+6,Y-4),15:LINE(X+6,Y+4)-(X+10,Y),S
194 LINE(X+5,Y+3)-(X+9,Y-1),S:LINE(X+3,Y+1)-(X+7,Y-3),15
195 RETURN
