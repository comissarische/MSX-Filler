10 POKE&HFBB1,1:screen0,,0
15 _TURBO ON
20 DEFINT A-Z:DIM CL(7),SCL(7),FLD(35,12),PLT(7,6),FCOL(7),ACT(2),CP(2),TTL(2),RS(4,2):DEFINT A-Z
30 CM=7:CR=0:DZ=1:BL=1:PZ=0:DZ=1:p$="t200o8m500s1l8cga":gosub1979
40 FOR I=1TO7:READCL(I),PLT(I,1),PLT(I,2),PLT(I,3),SCL(I),PLT(I,4), PLT(I,5),PLT(I,6):COLOR=(CL(I),PLT(I,1),PLT(I,2),PLT(I,3)):COLOR=(SCL(I),PLT(I,4),PLT(I,5),PLT(I,6)):NEXT I
60 screen 5:COLOR 15,0,0:CLS
80 ACT(1)=8:ACT(2)=10:BL=1
90 SET PAGE 0,1:COLOR 15,0,0:CLS
100 FOR I=1 TO 7:C=CL(I):S=SCL(I):X=I*15-10:Y=200:GOSUB1170:NEXT I
140 DATA 8,7,2,2,6,5,0,0, 10,7,7,2,14,5,5,2, 9,7,7,7,1,5,5,5, 3,2,7,2,2,0,5,0, 7,2,7,7,11,0,5,5, 5,2,2,7,4,0,0,5, 13,7,2,7,12,5,0,5
150 X=9:STP=6:Y=4
160 C=4:A$="F":GOSUB1670:X=X+STP-1
170 C=2:A$="i":GOSUB1670:X=X+STP-1
180 C=6:A$="l":GOSUB1670:X=X+STP-1
190 C=1:A$="l":GOSUB1670:X=X+STP
200 C=7:A$="e":GOSUB1670:X=X+STP+1
210 C=5:A$="r":GOSUB1670
220 SET PAGE 0,0
230 LINE(10,100)-(240,115),,B
240 IF INKEY$<>"" THEN GOTO 240
250 C=15:X=0:Y=200:A$="  > (c)Comissar 2022, v1.12 <  ":GOSUB1670
260 LINE (166,200)-(171,202),4,BF
270 LINE (166,203)-(171,205),10,BF
280 FOR J=4 TO 10
290 FOR I=8 TO 42
300 SET PAGE 0,1 :C=POINT(I,J):SET PAGE 0,0
310 IF C<>0 THEN X=I*7-58:Y=J*9-10:GOSUB1190
320 IF INKEY$<>"" THEN LINE(0,0)-(250,95),0,BF:I=42:J=10
330 NEXT I :LINE(15,105)-(J*32-86,110),15,BF
340 NEXT J
350 PC=1
360 LINE (40,145)-(210,182),15,B
370 LINE (41,146)-(209,181),0,BF
380 C=15:X=76:Y=150:A$="1 player":GOSUB1670
390 Y=160:A$="2 players":GOSUB1670
400 Y=170:A$="Settings":GOSUB1670:COPY(60,150)-STEP(120,32) TO (59,150),,TPSET
410 LINE(50,150)-(66,181),0,BF
420 x=60:Y=140+PC*10:A$=">":GOSUB1670:COPY(52,150)-STEP(10,32) TO (51,150),,TPSET 
430 GOSUB1810:A$=INKEY$:GOSUB1720:IF A$="" AND RS(1,2)=0 AND RS(2,2)=0 AND RS(3,2)=0 AND RS(4,2)=0 THEN GOTO 430 ELSE IF A$<>" " AND ASC(A$)<>13 AND RS(1,2)=0 AND RS(2,2)=0 THEN PC=PCMOD3+1:GOTO 410
440 IF PC<>3 THEN GOTO 580 else pc=1
450 line(40,145)-(210,192),15,b:LINE (41,146)-(209,191),0,BF
460 C=15:X=76:Y=150:IF DZ=1THEN A$="Standart" ELSE A$="Classic"
470 GOSUB1670
480 Y=160:C=15:IF BL=1 THEN A$="Blinking ON" ELSE A$="Blinking OFF"
482 GOSUB1670
485 Y=170:C=15:IF PZ THEN A$="Sound ON" ELSE A$="Sound OFF"
490 GOSUB1670
500 Y=180:A$="Back":GOSUB1670:COPY(60,150)-STEP(120,42) TO (59,150),,TPSET:C=CL(4):S=SCL(4):X=170:Y=155:GOSUB1170:C=15
510 LINE(50,150)-(66,191),0,BF
520 x=60:Y=140+PC*10:A$=">":GOSUB1670:COPY(52,150)-STEP(10,42) TO (51,150),,TPSET 
530 GOSUB1810:A$=INKEY$:GOSUB1720:IF A$="" AND RS(1,2)=0 AND RS(2,2)=0 AND RS(3,2)=0 AND RS(4,2)=0 THEN GOTO 530 ELSE IF A$<>" " AND ASC(A$)<>13 AND RS(1,2)=0 AND RS(2,2)=0 THEN PC=(PC and 3) + 1:GOTO 510
540 IF PC=1 THEN DZ=3-DZ
545 IF PC=2 THEN BL=1-BL
550 IF PC=3 THEN PZ=1-PZ:beep
560 IF PC=4 THEN SET PAGE 0,0:CLS:SET PAGE 0,1:GOTO 90
570 GOTO 450
580 G=RND(-TIME)
590 C=15:X=16:Y=130:A$=">   Generating crystals   <":GOSUB1670
600 FOR I=1 TO 34
610 FOR J=1 TO 11
620 FLD(I,J)=INT(RND(1)*7+1)
630 NEXT J:COLOR 15:LINE(15,105)-(I*6+30,110),4,BF
640 NEXT I
650 BIGAREA=90 '500 for big areas
660 C=15:X=16:Y=130:A$=">     Area prepairing     <":GOSUB1670
670 FOR I=2 TO BIGAREA
680 IF I MOD 10=0 THEN LINE(15,105)-(24*I/10+15,110),15,BF
690 X=RND(1)*(34-2)+2:Y=RND(1)*(11-2)+2:
700 IF I and 1=0 THEN FLD(X,Y)=FLD(X-1,Y-1) ELSE FLD(X,Y)=FLD(X-1,Y)
710 NEXT I
720 C=15:X=16:Y=130:A$=">     Prepairing field    <":GOSUB1670
730 NI=34:NJ=1
740 TTL(1)=1:TTL(2)=1
750 ST=1:FOR K=7 TO 1 STEP -1:IF FLD(2,11) <> K AND FLD(33,1)<>K THEN FCOL(ST)=K:ST=ST+1
760 NEXT K
770 K=INT(RND(TIME)*(7-2))+1:CP(1)=FCOL(K):CP(2)=FCOL(((K+2) MOD (7-2))+1):FLD(1,11)=CP(1):FLD(34,1)=CP(2)
780 SET PAGE 0,0:COLOR 15,0,0:CLS
790 FOR I=1 TO 7
800 C=CL(I):S=SCL(I):X=I*15-10:Y=200
810 SET PAGE 0,1:LINE(X-1,93)-(X+13,107),C,B:LINE(X+2,93)-(X+10,107),0,B :SET PAGE 0,0
820 GOSUB1170 :IF PC=2 THEN X=X+140:GOSUB1170
830 SET PAGE 0,1:LINE(X-1,93)-(X+13,107),C,B:LINE(X+2,93)-(X+10,107),0,B :SET PAGE 0,0
840 NEXT I
850 FOR I=1 TO 34
860 FOR J=1 TO 11 
870 GOSUB1130
880 NEXT J,I:FLD(1,11)=8:FLD(34,1)=10
900 TURN=2
910 TURN=3-TURN
920 IF PC=1 AND TURN=2 THEN GOSUB1410
930 IF PC=2 OR TURN=1 THEN GOSUB1270
940 CP(TURN)=IC
950 IF TURN=1 THEN ST=1:EN=34:S=1 ELSE ST=34:EN=1:S=-1
960 C=IC:X=95-90*S:Y=10:GOSUB1190
970 AD=0
980 FOR I=ST TO EN STEP S
990 S1=0
1000 FOR J=11 TO 1 STEP -1
1010 IF FLD(I,J)=ACT(TURN) THEN C=IC:GOSUB1140:S1=1
1020 IF FLD(I,J)=IC THEN GOSUB1220
1030 NEXT J
1040 IF S1=0 THEN I=EN
1050 NEXT I
1060 IF AD<>0 THEN 970
1070 C=15:X=110-90*S:Y=5:A$=STR$(TTL(TURN)):GOSUB1670
1080 IF TTL(1)=11*34/2 AND TTL(2)=11*34/2 THEN LINE (60,60)-(200,90),0,BF:LINE (60,60)-(200,90),15,B:X=100:Y=72:A$="D R A W !":GOSUB1670:COPY(80,72)-STEP(100,12) TO (79,72),,TPSET:GOTO 1100
1090 IF TTL(TURN) > 11*34/2 THEN LINE (60,60)-(200,90),0,BF:LINE (60,60)-(200,90),15,B:X=80:Y=72:A$="Player"+STR$(TURN)+" win!":GOSUB1670:COPY(80,72)-STEP(100,12) TO (79,72),,TPSET:ELSE GOTO 1110 
1100 A$=INKEY$:GOSUB1720:IF A$="" AND STRIG(1)=0 AND STRIG(2)=0 AND STRIG(3)=0 AND STRIG(4)=0 THEN GOTO 1100 ELSE SET PAGE 0,0:CLS:SET PAGE 0,1:GOTO 150
1110 GOTO 910
1120 END
1130 C=FLD(I,J) 
1140 X=I*7-3:Y=J*14+7*(Iand1)+15:GOTO 1190
1150 C=FLD(K,L)
1160 X=K*7-3:Y=L*14+7*(Kand1)+15:GOTO 1190
1170 IF DZ=1 THEN GOSUB1930 ELSE GOSUB1870
1180 RETURN
1190 if PZ=0 or ((peek(&hF959)-peek(&hF95A))>0) then goto1195 else k=rnd(1)*30:if K>3 then goto 1195 else if k <1 then p$="c":goto 1192 else if k<2 then p$="g":goto 1192
1191 p$="a"
1192 gosub 1979  
1195 COPY (C*15-10, 193)-STEP(13,13),1 TO (X,Y-6),,TPSET:RETURN
1200 IF PC=1 AND TURN=2 AND NI>I THEN NI=I:NJ=J
1210 FLD(I,J)=ACT(TURN):AD=1:S1=1:TTL(TURN)=TTL(TURN)+1:RETURN
1220 K=I+1:L=J:IF FLD(K,L)=ACT(TURN) THEN GOSUB1200:RETURN
1230 K=I+1:L=J+1-2*((I+1)and1):IF FLD(K,L)=ACT(TURN) THEN GOSUB1200:RETURN
1240 K=I-1:L=J:IF FLD(K,L)=ACT(TURN) THEN GOSUB1200:RETURN
1250 K=I-1:L=J+1-2*((I+1)and1):IF FLD(K,L)=ACT(TURN) THEN GOSUB1200:RETURN
1260 RETURN
1270 'get color for turn without cp(1) and cp(2) return ic
1280 CI=1:DC=1
1290 IF CP(1)=CI OR CP(2)=CI THEN CI=CI+DC :GOTO 1290
1300 IF CI>7 THEN CI=1 :GOTO 1290
1310 IF CI<1 THEN CI=7 :GOTO 1290
1320 X=CI*15 + (TURN) * 140 - 150:COPY(X-1,93)-(X+13,107),1 TO (X-1,193),,XOR
1330 GOSUB1810:A$=INKEY$:GOSUB1720:IF A$="" AND RS(1,2)=0 AND RS(2,2)=0 AND RS(3,2)=0 AND RS(4,2)=0 THEN GOTO 1330
1340 COLOR 0:X=CI*15 + (TURN) * 140 - 150:COPY(X-1,93)-(X+13,107),1 TO (X-1,193),,XOR
1350 IF A$=" " OR ASC(A$)=13 OR RS(1,2)<>0 OR RS(2,2)<>0 THEN IC=CI :COLOR=(15,7,7,7):RETURN 
1360 IF A$="Z" OR A$="z" OR A$="Q" OR A$="q" OR A$=CHR$(29) THEN DC=-1 ELSE DC=1
1370 CI=CI+DC:IF CI>7 THEN CI=1 
1380 IF CI<1 THEN CI=7
1390 GOTO 1290
1400 RETURN
1410 '
1420 Y1=0:Y2=0:Y3=0:Y4=0
1430 K=NI-1:L=NJ:IF CP(1)<>FLD(K,L) AND 0<>FLD(K,L) AND 7>=FLD(K,L)THEN I1=FLD(K,L):Y1=L
1440 K=NI-1:L=NJ+1-2*((NI+1)AND1):IF CP(1)<>FLD(K,L) AND 0 <> FLD(K,L) AND 7 >=FLD(K,L)THEN I2=FLD(K,L):Y2=L
1450 K=NI+1:L=NJ:IF CP(1)<>FLD(K,L) AND 0<>FLD(K,L) AND 7>=FLD(K,L)THEN I3=FLD(K,L):Y3=L
1460 K=NI+1:L=NJ+1-2*((NI+1)AND1):IF CP(1)<>FLD(K,L) AND 0 <> FLD(K,L) AND 7 >=FLD(K,L)THEN I4=FLD(K,L):Y4=L
1470 IF Y1=0 AND Y2=0 THEN GOTO 1500
1480 IF Y1>Y2 THEN IC=I1 ELSE IC=I2
1490 RETURN
1500 IF Y3=0 AND Y4=0 THEN GOTO 1530
1510 IF Y3>Y4 THEN IC=I3 ELSE IC=I4
1520 RETURN
1530 Y1=0:FOR I1=NI TO 34:FOR I2=11 TO 1 STEP -1
1540 IF FLD(I1,I2)=ACT(2) AND (0<>FLD(I1-1,I2) AND 7>=FLD(I1-1,I2) AND CP(1)<>FLD(I1-1,I2)) THEN NI=I1:NJ=I2:Y1=1:GOTO 1590
1550 IF FLD(I1,I2)=ACT(2) AND (0<>FLD(I1-1,I2+1-2*((I1+1)and1)) AND 7>=FLD(I1-1,I2+1-2*((I1+1)and1)) AND CP(1)<>FLD(I1-1,I2+1-2*((I1+1)and1))) THEN NI=I1:NJ=I2:Y1=1:GOTO 1590
1560 IF FLD(I1,I2)=ACT(2) AND (0<>FLD(I1+1,I2) AND 7>=FLD(I1+1,I2) AND CP(1)<>FLD(I1+1,I2)) THEN NI=I1:NJ=I2:Y1=1:GOTO 1590
1570 IF FLD(I1,I2)=ACT(2) AND (0<>FLD(I1+1,I2+1-2*((I1+1)and1)) AND 7>=FLD(I1+1,I2+1-2*((I1+1) and 1)) AND CP(1)<>FLD(I1+1,I2+1-2*((I1+1)and1))) THEN NI=I1:NJ=I2:Y1=1:GOTO 1590
1580 GOTO 1600
1590 I1=34:I2=1
1600 NEXT I2:NEXT I1:IF Y1<>0 THEN GOTO 1420
1610 IC=INT(RND(1)*7+1)
1620 IF CP(1)=IC THEN IC=IC+1 
1630 IF IC>7 THEN IC=1:GOTO 1620
1640 IF CP(2)=IC THEN IC=IC+1:GOTO 1620
1650 IF IC>7 THEN IC=1 :GOTO 1620
1660 RETURN
1670 ' SUB Put_String(a$,x,y,c)
1680 COLOR C:PRESET(X,Y):FOR I=1 TO LEN(A$):K=ASC(MID$(A$,I,1))
1690 '#I &h3A,K
1700 '#I &HCD,&H8D,&H00
1710 NEXT I:RETURN
1720 IF BL<>1 THEN RETURN 
1730 CR=CR+1:IF CR<1000 THEN RETURN
1740 CR=0:IF CM=7 THEN CM=6 ELSE CM=7
1750 COLOR=(15,CM,CM,CM)
1760 RETURN
1810 'SUB RealStrig() return RS(device,is_pressed=1/is_released=2)
1820 FOR I=0 TO 4
1830 A=STRIG(I):IF A=0 AND RS(I,1)=-1 THEN RS(I,2)=1 ELSE RS(I,2)=0
1840 RS(I,1)=A
1850 NEXT I
1860 RETURN
1870 LINE(X,Y)-(X+6,Y),S:LINE(X+6,Y)-(X+6,Y+6),S:LINE(X+6,Y+6)-(X,Y),S:PAINT(X+2,Y+1),S,S
1880 LINE(X+7,Y-1)-(X+12,Y-1),S:LINE(X+12,Y-1)-(X+7,Y-6),S:LINE(X+7,Y-6)-(X+7,Y-1),S:PAINT(X+8,Y-2),S,S
1890 LINE(X,Y-1)-(X+6,Y-1),C:LINE(X+6,Y-1)-(X+6,Y-7),C:LINE(X+6,Y-7)-(X,Y-1),C:PAINT(X+3,Y-2),C,C
1900 LINE(X+7,Y)-(X+7,Y+6),S:LINE(X+7,Y+6)-(X+13,Y),S:LINE(X+13,Y)-(X+7,Y),S:PAINT(X+8,Y+1),S,S
1910 PSET(X+7,Y),0:LINE(X+9,Y)-(X+7,Y+2),0:LINE(X+11,Y)-(X+7,Y+4),0:LINE(X+13,Y)-(X+7,Y+6),0
1920 RETURN
1930 LINE(X,Y)-(X+6,Y-6),S:LINE(X+6,Y-6)-(X+12,Y),C:LINE(X+12,Y)-(X+6,Y+6),C:LINE(X,Y)-(X+6,Y+6),C:LINE(X+1,Y)-(X+6,Y-5),C
1940 PAINT(X+4,Y),C,C:LINE(X+2,Y)-(X+6,Y-4),15:LINE(X+6,Y+4)-(X+10,Y),S
1950 LINE(X+5,Y+3)-(X+9,Y-1),S:LINE(X+3,Y+1)-(X+7,Y-3),15
1960 RETURN
1979 p$=CHR$(34) + p$ + CHR$(34)+CHR$(0)
1980 '#I &hBB,&h6D,&h21,p$,&h23,&hdd,&h2a,&hae,&h39,&hcd,&h59,&h01
1990 return